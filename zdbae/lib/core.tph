/* continuous chapter for EET */
ACTION_IF GAME_IS ~eet~ BEGIN
  OUTER_SET bg2_chapter = 12
END ELSE BEGIN
  OUTER_SET bg2_chapter = 0
END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
END

// GENIEHIT->ZDGHIT, GENIEDAM->ZDGDAM
COPY ~%MOD_FOLDER%/eff~ ~override~
// IBARING->ZDRING0
COPY ~%MOD_FOLDER%/bam~ ~override~
COPY ~%MOD_FOLDER%/sound~ ~override~
COPY ~%MOD_FOLDER%/portraits~ ~override~

// CD_STATE_NOTVALID
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

/// Items ///
// Barrityl's Burden
COPY ~%MOD_FOLDER%/items/zdring.itm~ ~override~
  SAY NAME1 @71 /* Small Ring */
  SAY NAME2 @72 /* Barrityl's Burden */
  SAY UNIDENTIFIED_DESC @73 /* This ring promises power at a great cost. */
  SAY DESC @74 /* Created by a djinni as a "gift" to the drow sorcerer Baeloth Barrityl, this obsidian ring with blood-red ruby almost hypnotically draws the eye to it. Acting as additional storage space for magical knowledge, once placed upon a spellcaster's finger it imbues [him] with the ability to learn additional spells beyond what [his] skill normally allows.[2] It also gives the wearer the chance to spontaneously create wealth once a day in the form of some gemstone, but that is a double-edged sword; should the gem fail to materialize, the wearer endures a period of searing pain.

  The djinni that created the ring, not entirely trusting the person to whom it was gifted, wove a curse into the black stone band. In battle with a djinni, the ring's wearer will find [him]self at an extreme disadvantage. Once donned, it cannot be removed from its wearer's finger, no matter how much [he] might wish it so.

  Charge abilities:
  - Create a random gem or damage the wearer, once per rest

  Equipped abilities:
  - Can cast two extra spells of each level
  - THAC0: -5 penalty vs Genies
  - Damage: -5 penalty vs Genies */

// Barrityl's Fur Boots
COPY ~%MOD_FOLDER%/items/zdbo01.itm~ ~override~
  SAY NAME1 @76 /* Fur Boots */
  SAY NAME2 @77 /* Barrityl's Fur Boots */
  SAY UNIDENTIFIED_DESC @78 /* A closer inspection of these boots leaves you more confused then when you started. */
  SAY DESC @79 /* As expected from Baeloth, these leather fur boots are outrageously extravagant, expensive, and unique. The leather is deep, almost black, with an unusual texture that shifts in the light—somehow both smooth and rough at the same time. The dark fur lining is soft to the touch, yet surprisingly warm, providing an eerie contrast to the cold, smooth exterior. The fur appears to be woven into the leather, almost like it’s part of the material itself, giving the boots an added layer of mystery. Intricate stitching along the seams is done in a fine silver thread, adding an elegant, almost regal touch to the otherwise imposing design.

  Baeloth often loudly remarks that these boots are a fusion of function and form, as they are magically enhanced, somehow conferring him with even more confidence and speechcraft, while protecting him from others' attempts to persuade or charm him.

  Equipped abilities:
  - Charisma: +1
  - Saving Throws: +1
  - Wearer is immune to Charm
  - Protects against critical hits */

// Barrityl's Fabulous Leather Boots
COPY ~%MOD_FOLDER%/items/zdbo02.itm~ ~override~
  SAY NAME1 @80 /* Leather Boots */
  SAY NAME2 @81 /* Barrityl's Fabulous Boots */
  SAY UNIDENTIFIED_DESC @78 /* A closer inspection of these boots leaves you more confused then when you started. */
  SAY DESC @82 /* As expected from Baeloth, these leather boots are outrageously extravagant, expensive, and unique. Crafted from leather that seems to shift in color, they range from a deep, earthy brown to subtle hues of amber, changing with every step and angle, as though alive with some hidden magic. The texture is unlike any normal leather—smooth in some areas, rugged in others, giving the boots an almost sentient quality. The surface catches the light in curious ways, creating an illusion that the boots are constantly in motion, even when standing still.

  Baeloth loudly remarks how gracefully grounded he feels. The boots grant an unnatural speed, enhancing agility and reflexes, while also imbuing him with immunity to movement-restricting spells.

  Equipped abilities:
  - Immunity to Stun, Hold Person and Paralyse
  - Doubles movement rate
  - Protects against critical hits */

// Barrityl's Simple Boots
COPY ~%MOD_FOLDER%/items/zdbo03.itm~ ~override~
  SAY NAME1 @80 /* Leather Boots */
  SAY NAME2 @83 /* Barrityl's Simple Boots */
  SAY UNIDENTIFIED_DESC @78 /* A closer inspection of these boots leaves you more confused then when you started. */
  SAY DESC @84 /* These boot elicit a sigh from Baeloth, as these boots are simple yet effective. A slightly upgraded version of boots of speed, but much to Baeloth's chagrin they are not unique or rare enough for him to feel noticed.

  Equipped abilities:
  - Charisma: -1
  - Doubles movement rate
  - Protects against critical hits */

/// Creature files ///
// SoA
COPY ~%MOD_FOLDER%/cre/zdbae.cre~ ~override~
  SAY NAME1 @99
  SAY NAME2 @99
  SAY BIO @100
  SAY BATTLE_CRY1 @1
  SAY BATTLE_CRY2 @14
  SAY BATTLE_CRY3 @15
  SAY BATTLE_CRY4 @16
  SAY BATTLE_CRY5 @17
  SAY LEADER @2
  SAY TIRED @3
  SAY BORED @4
  SAY HURT @5
  SAY DAMAGE @12
  SAY DYING @13
  SAY MORALE @18
  SAY HAPPY @19
  SAY UNHAPPY_ANNOYED @20
  SAY UNHAPPY_SERIOUS @21
  SAY UNHAPPY_BREAKING @22
  SAY SELECT_COMMON1 @6
  SAY SELECT_COMMON2 @7
  SAY SELECT_COMMON3 @8
  SAY SELECT_COMMON4 @23
  SAY SELECT_COMMON5 @24
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @9
  SAY SELECT_ACTION2 @10
  SAY SELECT_ACTION3 @11
  SAY SELECT_ACTION4 @26
  SAY SELECT_ACTION5 @27
  SAY SELECT_ACTION6 @28
  SAY REACT_TO_DIE_GENERAL @29
  SAY REACT_TO_DIE_SPECIFIC @30
  SAY SELECT_RARE1 @31
  SAY SELECT_RARE2 @32
  SAY CRITICAL_HIT @33
  SAY CRITICAL_MISS @34
  SAY TARGET_IMMUNE @35
  SAY INVENTORY_FULL @36
  SAY PICKED_POCKET @37
  SAY HIDDEN_IN_SHADOWS @38
  SAY SPELL_DISRUPTED @39
  SAY SET_A_TRAP @40
  WRITE_ASCII PORTRAIT_LARGE ~zdbael~ #8
  WRITE_ASCII PORTRAIT_SMALL ~zdbaem~ #8
  WRITE_ASCII SCRIPT_OVERRIDE ~zdbaes~ #8
  WRITE_ASCII DEATHVAR ~zdbae~ #32
  WRITE_ASCII DIALOG ~zdbae~ #8
  REMOVE_CRE_ITEMS
  ADD_CRE_ITEM ~CLCK17~ #0 #0 #0 ~identified&stolen~ ~armor~ EQUIP
  ADD_CRE_ITEM ~STAF18~ #0 #0 #0 ~identified&stolen~ ~weapon1~ EQUIP
  ADD_CRE_ITEM ~ZDRING~ #0 #0 #0 ~identified&undroppable~ ~rring~ EQUIP

// ToB
COPY ~%MOD_FOLDER%/cre/zdbae.cre~ ~override/ZDBAE25.CRE~
  SAY NAME1 @99
  SAY NAME2 @99
  SAY BIO @100
  SAY BATTLE_CRY1 @1
  SAY BATTLE_CRY2 @14
  SAY BATTLE_CRY3 @15
  SAY BATTLE_CRY4 @16
  SAY BATTLE_CRY5 @17
  SAY LEADER @2
  SAY TIRED @3
  SAY BORED @4
  SAY HURT @5
  SAY DAMAGE @12
  SAY DYING @13
  SAY MORALE @18
  SAY HAPPY @19
  SAY UNHAPPY_ANNOYED @20
  SAY UNHAPPY_SERIOUS @21
  SAY UNHAPPY_BREAKING @22
  SAY SELECT_COMMON1 @6
  SAY SELECT_COMMON2 @7
  SAY SELECT_COMMON3 @8
  SAY SELECT_COMMON4 @23
  SAY SELECT_COMMON5 @24
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @9
  SAY SELECT_ACTION2 @10
  SAY SELECT_ACTION3 @11
  SAY SELECT_ACTION4 @26
  SAY SELECT_ACTION5 @27
  SAY SELECT_ACTION6 @28
  SAY REACT_TO_DIE_GENERAL @29
  SAY REACT_TO_DIE_SPECIFIC @30
  SAY SELECT_RARE1 @31
  SAY SELECT_RARE2 @32
  SAY CRITICAL_HIT @33
  SAY CRITICAL_MISS @34
  SAY TARGET_IMMUNE @35
  SAY INVENTORY_FULL @36
  SAY PICKED_POCKET @37
  SAY HIDDEN_IN_SHADOWS @38
  SAY SPELL_DISRUPTED @39
  SAY SET_A_TRAP @40
  WRITE_ASCII PORTRAIT_LARGE ~zdbael~ #8
  WRITE_ASCII PORTRAIT_SMALL ~zdbaem~ #8
  WRITE_ASCII SCRIPT_OVERRIDE ~zdbae25s~ #8
  WRITE_ASCII DEATHVAR ~zdbae~ #32
  WRITE_ASCII DIALOG ~zdbae25~ #8
  REMOVE_CRE_ITEMS
  ADD_CRE_ITEM ~CLCK17~ #0 #0 #0 ~identified&stolen~ ~armor~ EQUIP
  ADD_CRE_ITEM ~STAF18~ #0 #0 #0 ~identified&stolen~ ~weapon1~ EQUIP
  ADD_CRE_ITEM ~ZDRING~ #0 #0 #0 ~identified&undroppable~ ~rring~ EQUIP

/// Dialog ///

// Compile dialog
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialog~

// Append tob banter with sod banter
COPY ~%MOD_FOLDER%/dialog/zdbaeb.d~ ~weidu_external/%MOD_FOLDER%/compile/zdbaeb.d~
  REPLACE_TEXTUALLY ~BEGIN ZDBAEB~ ~~
  REPLACE_TEXTUALLY ~== ZDBAEB~ ~== ZDBAE25B~
COPY ~%MOD_FOLDER%/dialog/zdbae25b.d~ ~weidu_external/%MOD_FOLDER%/compile/zdbae25b.d~
  APPEND_FILE ~weidu_external/%MOD_FOLDER%/compile/zdbaeb.d~
COMPILE EVALUATE_BUFFER ~weidu_external/%MOD_FOLDER%/compile/zdbae25b.d~

// Scripts
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/zdbaes.baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/zdbae25s.baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/zdbaed.baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/zdbae25d.baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/zbcutrel.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~baldur.bcs~ THEN BEGIN
EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/baf/rep.baf~
END
ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN
EXTEND_TOP ~baldur25.bcs~ ~%MOD_FOLDER%/baf/rep.baf~
END

// Appending dialog files
// See [here](https://www.gibberlings3.net/forums/topic/15833-dialog-files-and-how-they-are-referenced/)
// cre post_dialog_file join_dialog_file dream_script_file 25post_dialog_file 25join_dialog_file 25dream_script_file 25override_script_file
APPEND ~pdialog.2da~
  ~zdbae zdbaep zdbaej zdbaed zdbae25p zdbae25j zdbae25d zdbae25~
UNLESS ~zdbae~

// Banter
// cre FILE 25FILE
APPEND ~interdia.2da~
  ~zdbae zdbaeb zdbae25b~
UNLESS ~zdbae~

// Epilogue
COPY ~%MOD_FOLDER%/epilogue/zdbae.2da~ ~override/zdbae01.2da~
REPLACE ~99999~ @60

// Spawn Baeloth in the Copper Cornet
EXTEND_TOP ~AR0406.BCS~ ~%MOD_FOLDER%/baf/ar0406.baf~
  EVALUATE_BUFFER

// Add Baeloth to ToB
ACTION_IF GAME_IS ~eet~ THEN BEGIN // EET related code & summoning script
  INCLUDE ~EET/other/EET_functions.tph~ //declaration of external EET functions used below
  LAF ~EET_NPC_TRANSITION~ //function used to automatically implement EET continuity system
    INT_VAR
      type = 3 // NPC with BG1 and BG2 content (full transition to SoA/ToB)
      default_ToB = 1 // Summonable in ToB
    STR_VAR
      dv = "BAELOTH" // Death Variable (script name)
      override_BG1 = "BAELOTH" // name of the override NPC script in BG1
      override_SoD = "BAELOTH" // name of the override NPC script in SoD (use BG1 one if doesn't exist)
      override_SoA = "ZDBAEs" // SoA Override script
      override_ToB = "ZDBAE25s" // ToB Override script
      dialog_ToB = "ZDBAE25" // ToB dialogue file
      cre_ToB = "ZDBAE25"
      traFile = EVAL "%MOD_FOLDER%/tra/%LANGUAGE%/setup.tra"
      string = "@5999" // Bring me Baeloth, the drow sorcerer.
      stringPosDV = "Baeloth" // Baeloth
  END
END ELSE BEGIN
  COMPILE ~%MOD_FOLDER%/fate/fatesp.d~
END
