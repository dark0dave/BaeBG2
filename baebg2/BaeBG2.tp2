BACKUP ~baebg2/backup~
AUTHOR ~dark0dave~

VERSION ~v0.2~
AUTO_TRA ~%MOD_FOLDER%/tra/%s~

LANGUAGE ~English~
         ~english~
         ~%MOD_FOLDER%/tra/english/setup.tra~

BEGIN ~Baeloth BG2~
DESIGNATED 0 LABEL ~baebg2~
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This mod requires BG2EE or EET~

/* continuous chapter for EET */
ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
        OUTER_SET bg2_chapter = bg2_chapter + 1
        OUTER_SPRINT name_source ~bg2_chapter_%i%~
        OUTER_SET EVAL ~%name_source%~ = bg2_chapter
END

COPY ~%MOD_FOLDER%/misc~ ~override~
COPY ~%MOD_FOLDER%/sound~ ~override~

ACTION_IF NOT FILE_EXISTS ~override/BAELOTHL.BMP~ BEGIN
    COPY ~%MOD_FOLDER%/portraits/BAELOTHL.BMP~ ~override~
END

ACTION_IF NOT FILE_EXISTS ~override/BAELOTHM.BMP~ BEGIN
    COPY ~%MOD_FOLDER%/portraits/BAELOTHM.BMP~ ~override~
END

ACTION_IF NOT FILE_EXISTS ~override/BAELOTHS.BMP~ BEGIN
    COPY ~%MOD_FOLDER%/portraits/BAELOTHS.BMP~ ~override~
END

/// Dialog ///

// Compile dialog
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25B.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25D.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25J.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25P.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAE25S.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAEB.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAED.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAEJ.D~
COMPILE ~%MOD_FOLDER%/dialog/BG2BAEP.D~

// Scripts
COMPILE ~%MOD_FOLDER%/baf/BG2BAE.BAF~
COMPILE ~%MOD_FOLDER%/baf/BG2BAE25.BAF~

// Appending dialog files
// (https://www.gibberlings3.net/forums/topic/15833-dialog-files-and-how-they-are-referenced/)
APPEND ~pdialog.2da~
    ~BG2BAE BG2BAEP BG2BAEJ BG2BAED BG2BAE25P BG2BAE25J BG2BAE25D BG2BAE25S~
UNLESS ~BG2BAE~

// Banter
APPEND ~interdia.2da~
    ~BG2BAE BG2BAEB BG2BAEB~
UNLESS ~BG2BAE~

// Creature files //
// SoA
COPY ~%MOD_FOLDER%/cre/BG2BAE.CRE~ ~override/BG2BAE.CRE~
SAY BIO @100
SAY BATTLE_CRY1 @001
SAY BATTLE_CRY2 @014
SAY BATTLE_CRY3 @015
SAY BATTLE_CRY4 @016
SAY BATTLE_CRY5 @017
SAY LEADER @002
SAY TIRED @003
SAY BORED @004
SAY HURT @005
SAY DAMAGE @012
SAY DYING @013
SAY MORALE @018
SAY HAPPY @019
SAY UNHAPPY_ANNOYED @020
SAY UNHAPPY_SERIOUS @021
SAY UNHAPPY_BREAKING @022
SAY SELECT_COMMON1 @006
SAY SELECT_COMMON2 @007
SAY SELECT_COMMON3 @008
SAY SELECT_COMMON4 @023
SAY SELECT_COMMON5 @024
SAY SELECT_COMMON6 @025
SAY SELECT_ACTION1 @009
SAY SELECT_ACTION2 @010
SAY SELECT_ACTION3 @011
SAY SELECT_ACTION4 @026
SAY SELECT_ACTION5 @027
SAY SELECT_ACTION6 @028
SAY REACT_TO_DIE_GENERAL @029
SAY REACT_TO_DIE_SPECIFIC @030
SAY SELECT_RARE1 @031
SAY SELECT_RARE2 @032
SAY CRITICAL_HIT @033
SAY CRITICAL_MISS @034
SAY TARGET_IMMUNE @035
SAY INVENTORY_FULL @036
SAY PICKED_POCKET @037
SAY HIDDEN_IN_SHADOWS @038
SAY SPELL_DISRUPTED @039
SAY SET_A_TRAP @040
WRITE_ASCII SCRIPT_OVERRIDE ~bg2bae~  #8
WRITE_ASCII 0x280 ~bg2bae~  #32
WRITE_ASCII DIALOG ~bg2bae~  #8
WRITE_ASCII 0X34 ~BAELOTHS~ #8   //medium portrait
WRITE_ASCII 0x3c ~BAELOTHL~ #8   //large portrait

// ToB
COPY ~%MOD_FOLDER%/cre/BG2BAE.CRE~ ~override/BG2BAE25.CRE~
SAY BIO @100
SAY BATTLE_CRY1 @001
SAY BATTLE_CRY2 @014
SAY BATTLE_CRY3 @015
SAY BATTLE_CRY4 @016
SAY BATTLE_CRY5 @017
SAY LEADER @002
SAY TIRED @003
SAY BORED @004
SAY HURT @005
SAY DAMAGE @012
SAY DYING @013
SAY MORALE @018
SAY HAPPY @019
SAY UNHAPPY_ANNOYED @020
SAY UNHAPPY_SERIOUS @021
SAY UNHAPPY_BREAKING @022
SAY SELECT_COMMON1 @006
SAY SELECT_COMMON2 @007
SAY SELECT_COMMON3 @008
SAY SELECT_COMMON4 @023
SAY SELECT_COMMON5 @024
SAY SELECT_COMMON6 @025
SAY SELECT_ACTION1 @009
SAY SELECT_ACTION2 @010
SAY SELECT_ACTION3 @011
SAY SELECT_ACTION4 @026
SAY SELECT_ACTION5 @027
SAY SELECT_ACTION6 @028
SAY REACT_TO_DIE_GENERAL @029
SAY REACT_TO_DIE_SPECIFIC @030
SAY SELECT_RARE1 @031
SAY SELECT_RARE2 @032
SAY CRITICAL_HIT @033
SAY CRITICAL_MISS @034
SAY TARGET_IMMUNE @035
SAY INVENTORY_FULL @036
SAY PICKED_POCKET @037
SAY HIDDEN_IN_SHADOWS @038
SAY SPELL_DISRUPTED @039
SAY SET_A_TRAP @040
WRITE_ASCII SCRIPT_OVERRIDE ~bg2bae25~  #8
WRITE_ASCII 0x280 ~bg2bae25~  #32
WRITE_ASCII DIALOG ~bg2bae25~  #8
WRITE_ASCII 0X34 ~BAELOTHS~ #8   //medium portrait
WRITE_ASCII 0x3c ~BAELOTHL~ #8   //large portrait

/// Items ///
COPY ~%MOD_FOLDER%/items/BARING.ITM~ ~override/BARING.ITM~
SAY NAME1 @91
SAY NAME2 @91
SAY UNIDENTIFIED_DESC @92
SAY DESC  @92

COPY ~%MOD_FOLDER%/items/BAPOTION.ITM~ ~override/BAPOTION.ITM~
SAY NAME1 @93
SAY NAME2 @93
SAY UNIDENTIFIED_DESC @94
SAY DESC  @94

// Spawn Baeloth in Red Wizard Enclave
EXTEND_TOP ~OH6300.BCS~ ~%MOD_FOLDER%/baf/SPAWNBG2BAE.BAF~

// LCA compatibility
ACTION_IF FILE_EXISTS_IN_GAME ~xacorwin.cre~ AND FILE_EXISTS_IN_GAME ~xacaelar.cre~
THEN BEGIN
  PRINT ~Adding compatibility for LCA~
COMPILE ~%MOD_FOLDER%/dialogue/crossmod/LCA.D~
END

// Add Baeloth to ToB
ACTION_IF GAME_IS ~eet~ THEN BEGIN // EET related code & summoning script
  INCLUDE ~EET/other/EET_functions.tph~ //declaration of external EET functions used below
  LAF ~EET_NPC_TRANSITION~ //function used to automatically implement EET continuity system
    INT_VAR
      type = 3 // NPC with BG1 and BG2 content (full transition to SoA/ToB)
      default_ToB = 1 // Summonable in ToB
    STR_VAR
      dv = "BAELOTH" // Death Variable (script name)
      override_BG1 = "BAELOTH" // name of the override NPC script in BG1
      override_SoD = "BAELOTH" // name of the override NPC script in SoD (use BG1 one if doesn't exist)
      override_SoA = "BG2BAE" // SoA Override script
      override_ToB = "BG2BAE25" // ToB Override script
      dialog_ToB = "BG2BAE25" // ToB dialogue file
      cre_ToB = "BG2BAE25"
      traFile = EVAL "AllThingsMazzy/translations/%LANGUAGE%/setup.tra"
      string = "@9999" // Bring me Baeloth, the drow sorcerer.
      stringPosDV = "Baeloth" // Baeloth
  END
END ELSE BEGIN
    COMPILE ~%MOD_FOLDER%/dialog/FATESP.D~
END
